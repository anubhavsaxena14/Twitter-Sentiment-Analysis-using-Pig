REGISTER /usr/lib/pig/piggybank.jar; 
extract_details = LOAD '/user/cloudera/data/AD1.csv' using org.apache.pig.piggybank.storage.CSVLoader()  AS (id:long,text:chararray);
tokens = foreach extract_details generate id,text, FLATTEN(TOKENIZE(text)) As word;
dictionary = load '/user/cloudera/data/AFINN.txt' using PigStorage('\t') AS(word:chararray,rating:int);
word_rating = join tokens by word left outer, dictionary by word using 'replicated';
describe word_rating;
rating = foreach word_rating generate tokens::id as id,tokens::text as text, dictionary::rating as rate;
word_group = group rating by (id,text);
avg_rate = foreach word_group generate group, AVG(rating.rate) as tweet_rating;
STORE avg_rate INTO 'SentimentAnalysis' using PigStorage(',');
